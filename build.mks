#!/usr/bin/env mks

// env settings
setenv_default("SITE_ENV", "devel")
env = getenv("SITE_ENV")
log("Build env %q", env)

// default params
params = params_new()
params["base_path"] = ""
params["subtitle"] = "Respira"
params["author"] = "Editor"
params["site_url"] = "http://localhost:8000"
params["current_year"] = datetime().Year()
params["theme"] = "w3css"

// load env params
params.Load("config/"+env+".json")
theme = "theme/" + params["theme"]

// start from scratch
rmtree("_site")

// sync static files
copytree("static", "_site/static")
copytree(theme+"/static", "_site/static")

// build i18n site
func build(params) {
	lang = params["lang"]
	log("Build lang %q", lang)
	src = "content"
	dst = "_site"
	params["lang_url"] = "/en"
	params["lang_change"] = "en"
	params["lang_info"] = "English"
	if lang != "es" {
		src = "content/" + lang
		dst = "_site/" + lang
		params["base_path"] = "/" + lang
		params["lang_url"] = ""
		params["lang_change"] = "es"
		params["lang_info"] = "Espa√±ol"
	}
	// load layouts
	menu_content = fread(src+"/_menu.html")
	copyright_text = fread(src+"/copyright.txt")
	page_layout = fread(theme+"/layout/page.html")
	// combine layouts
	params["menu_content"] = menu_content
	params["copyright_text"] = copyright_text
	//~ log("Params %v", params)
	page_layout = render(page_layout, params)
	// site pages
	if lang == "es" { // create 404.html only once
		make_pages(src+"/_404.md", dst+"/404.html", page_layout, params)
	}
	make_pages(src+"/_index.md", dst+"/index.html", page_layout, params)
	make_pages(src+"/[^_]*.md", dst+"/{{ slug }}/index.html", page_layout, params)
}

for lang in ["es", "en"] {
	params["lang"] = lang
	build(params)
}
